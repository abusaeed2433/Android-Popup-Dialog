/*
 * Copyright 2018-2024 Saad Ahmed
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'com.android.library'
    id 'maven-publish'
    id 'signing'
}

android {
    namespace 'com.saadahmedev.popupdialog'
    compileSdk 34

    defaultConfig {
        minSdk 21
        targetSdk 34

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    publishing {
        singleVariant("release") {
            withSourcesJar()
            withJavadocJar()
        }
    }

    buildFeatures {
        dataBinding true
        viewBinding true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
}

def artifactVersion = "2.0.0-SNAPSHOTS"

publishing {
    publications {
        maven(MavenPublication) {
            groupId 'com.saadahmedev.popup-dialog'
            artifactId 'popup-dialog'
            version artifactVersion
            artifact("$buildDir/outputs/aar/popupDialog-sources.jar")
            artifact("$buildDir/outputs/aar/popupDialog-release.aar")

            pom {
                name = 'Android Popup Dialog'
                description = 'A custom android popup dialog library which provides you a lot of popup dialog with and without animation'
                url = 'https://github.com/saadahmedscse/Android-Popup-Dialog'

                withXml {
                    def node = asNode().appendNode('dependencies').appendNode('dependency')
                    node.appendNode('groupId', 'com.airbnb.android')
                    node.appendNode('artifactId', 'lottie')
                    node.appendNode('version', '5.2.0')
                    node.appendNode('scope', 'compile')
                }

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'saadahmedscse'
                        name = 'Saad Ahmed'
                        email = 'saadahmedscse@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/saadahmedscse/Android-Popup-Dialog.git'
                    developerConnection = 'scm:git:ssh://github.com:saadahmedscse/Android-Popup-Dialog.git'
                    url = 'https://github.com/saadahmedscse/Android-Popup-Dialog'
                }
            }
        }
    }
    repositories {
        maven {
            def releaseUrl = "https://s01.oss.sonatype.org/content/repositories/releases/"
            def snapshotUrl = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
            url = artifactVersion.endsWith('SNAPSHOT') ? snapshotUrl : releaseUrl

            credentials {
                username("username")
                password("password")
            }
        }
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.11.0'

    implementation "com.airbnb.android:lottie:5.2.0"
}

signing {
    sign configurations.archives
}